

: REGISTER_TCS34725{ \ ( -- ADDR )
  0 ;

: REGISTER ( ADDR -- ADDR )
  DUP 1+ SWAP CONSTANT ;

: SKIP ( ADDR -- ADDR )
  1+ ;

: }REGISTERS_TCS34725 \ ( ADDR -- )
  DROP ;

REGISTER_TCS34725{
  REGISTER REG_ENABLE
  REGISTER ATIME
  SKIP
  REGISTER WTIME
  REGISTER AILTL
  REGISTER AILTH
  REGISTER AIHTL
  REGISTER AIHTH
  SKIP
  SKIP
  SKIP
  SKIP
  REGISTER PERS
  REGISTER CONFIG
  SKIP
  REGISTER CONTROL
  SKIP
  SKIP
  REGISTER ID
  REGISTER STATUS
  REGISTER CDATAL
  REGISTER CDATAH
  REGISTER RDATAL
  REGISTER RDATAH
  REGISTER GDATAL
  REGISTER GDATAH
  REGISTER BDATAL
  REGISTER BDATAH
}REGISTERS_TCS34725


: BIT_REGISTER_ENABLE{ \ ( -- BIT )
  1 ;

: BIT ( BIT -- BIT )
  DUP 2* SWAP CONSTANT ;

: SKIP ( BIT -- BIT )
  2* ;

: }BIT_REGISTER_ENABLE ( BIT -- )
  DROP ;

BIT_REGISTER_ENABLE{
  BIT ENABLE_PON
  BIT ENABLE_AEN
  SKIP
  BIT ENABLE_WEN
  BIT ENABLE_AIEN
}BIT_REGISTER_ENABLE


80 CONSTANT COMMAND_BIT
29 CONSTANT SLAVE_ADDRESS

0 CONSTANT R
1 CONSTANT G
2 CONSTANT B

VARIABLE SUM
VARIABLE MAX
VARIABLE MIN

CREATE RGB_COMPONENT 0 , 0 , 0 , DOES> SWAP CELLS + ;


: WRITE ( VALUE #BYTES -- )
  SLAVE_ADDRESS SWAP I2C_WRITE ;

: READ ( #BYTES -- VALUE )
  SLAVE_ADDRESS SWAP I2C_READ ;

: READ8 \ ( REGISTER -- VALUE )
  COMMAND_BIT OR 1 WRITE 1 READ ;

: WRITE8 \ ( VALUE REGISTER -- )
  COMMAND_BIT OR 1 WRITE 1 WRITE ;

: READ16 \ ( REGISTER -- VALUE )
  COMMAND_BIT OR 1 WRITE 2 READ 8 LSHIFT OR ;

: POWER_ON ( -- )
  ENABLE_PON REG_ENABLE WRITE8
  3 MILLISECONDS DELAY \ 2.4 ms must pass after PON is asserted before an RGBC can be initiated
  ENABLE_PON ENABLE_AEN OR REG_ENABLE WRITE8 ;

: POWER_OFF ( -- )
  REG_ENABLE READ8 FC AND REG_ENABLE WRITE8 ;

: CONFIGURATION ( -- )
  I2C_INIT
  3 SECONDS DELAY
  POWER_ON ;

: CALCULATE_MIN ( R G B -- )
  DUP MIN !
  OVER > IF DUP MIN ! THEN DROP
  DUP MIN @ < IF DUP MIN ! THEN DROP ;


: CALCULATE_MAX ( R G B -- )
  DUP MAX !
  OVER < IF DUP MAX ! THEN DROP
  DUP MAX @ > IF DUP MAX ! THEN DROP ;

: RESET_COMPONENTS ( -- )
  0 R RGB_COMPONENT !
  0 G RGB_COMPONENT !
  0 B RGB_COMPONENT ! ;


: GET_RGB ( -- )
  RDATAL READ16
  GDATAL READ16
  BDATAL READ16
  CDATAL READ16
  DUP 0= IF
    2DROP 2DROP
    RESET_COMPONENTS
  ELSE
    SUM !
    FF * SUM @ /
    ROT FF * SUM @ /
    ROT FF * SUM @ /
    DUP 2OVER CALCULATE_MIN
    MIN @ - G RGB_COMPONENT !
    MIN @ - R RGB_COMPONENT !
    MIN @ - B RGB_COMPONENT !
  THEN ;
